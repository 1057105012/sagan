plugins {
	id 'java'
	id 'org.springframework.boot' version '2.4.2'
	id 'org.asciidoctor.convert' version '2.4.0'
	id 'com.gorylenko.gradle-git-properties' version '2.2.4'
}

apply plugin: 'io.spring.dependency-management'
apply from: "${rootDir}/gradle/publications.gradle"

group = 'io.spring.sagan'
version = "1.0.0-SNAPSHOT"
sourceCompatibility = '1.8'

ext {
	snippetsDir = file('build/generated-snippets')
}

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
}

repositories {
	mavenCentral()
}

dependencyManagement {
    generatedPomCustomization {
        enabled = false
    }
}

dependencies {
	compile project(':sagan-client')

	compile 'org.springframework.boot:spring-boot-starter-data-jpa'
	compile 'org.hibernate:hibernate-java8'
	compile 'org.springframework.boot:spring-boot-starter-data-redis'
	compile 'org.springframework.boot:spring-boot-starter-web'
	compile 'org.springframework.boot:spring-boot-starter-validation'
	compile 'org.springframework.boot:spring-boot-starter-hateoas'
	compile 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.security:spring-security-oauth2-client'
	implementation 'org.springframework.security:spring-security-oauth2-jose'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	// for use in on-the-fly database setup and migrations
	compile 'org.flywaydb:flyway-core'
	compile 'org.apache.httpcomponents:httpclient'
	compile 'org.modelmapper:modelmapper:2.3.9'
	compile 'org.jsoup:jsoup:1.9.2'
	compile 'org.yaml:snakeyaml'
	// for XML manipulation
	compile 'org.xmlbeam:xmlprojector:1.4.18'
	// for use in generating blog atom feeds
	compile 'com.rometools:rome:1.15.0'
	// for use in serving redirects; see urlrewrite.xml
	compile 'org.tuckey:urlrewritefilter:4.0.4'

	// Thymeleaf is used as the template engine for serving HTML
	implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time'
	implementation 'com.github.mxab.thymeleaf.extras:thymeleaf-extras-data-attribute'

	// for CloudFoundry service binding
	compile "io.pivotal.cfenv:java-cfenv-boot:2.2.5.RELEASE"

	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	// datasource and connection pool dependencies
	runtimeOnly 'mysql:mysql-connector-java'
	runtimeOnly 'com.h2database:h2'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	testCompile "org.testcontainers:testcontainers:1.15.1"
	testCompile "org.testcontainers:junit-jupiter:1.15.1"

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation(platform("org.springframework.cloud:spring-cloud-contract-dependencies:3.0.1"))
	testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'
}

test {
	useJUnitPlatform()
	outputs.dir snippetsDir
}

asciidoctor {
	attributes 'snippets': snippetsDir
	inputs.dir snippetsDir
	dependsOn test
}

asciidoctor.doLast {
    file("$buildDir/asciidoc/html5").renameTo(file("$buildDir/asciidoc/restdocs"))
}

bootJar {
	dependsOn asciidoctor
	from("${asciidoctor.outputDir}/restdocs") {
		into 'static/restdocs'
	}
}

task stubsJar(type: Jar) {
	archiveClassifier.set("stubs")
	into('/sagan/site/webapi/') {
		include('**/*Metadata.*')
		from("${project.projectDir}/src/main/java/sagan/site/webapi/")
		from("${project.buildDir}/classes/java/main/sagan/site/webapi/")
	}
	into("META-INF/${project.group}/${project.name}/${project.version}/mappings") {
		include('**/*.*')
		from("${project.buildDir}/generated-snippets/stubs")
	}
}

build.dependsOn("stubsJar")

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact stubsJar
		}
	}
}